apply plugin: "java"
apply plugin: "maven"
apply plugin: "signing"

group = "com.glookast.commons"
version = "3.2.0-RC1-SNAPSHOT"

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    manifest {
        attributes "Implementation-Version": version
    }
}

repositories {
    mavenCentral()
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

configurations {
    schemas
}

dependencies {
    schemas "com.glookast.commons:commons-base-schemas:2.0.0"
}

def generatedResourcesDir = file("generated/main/resources")

sourceSets.main.resources.srcDirs generatedResourcesDir

task extractSchemas(type: Copy) {
    from {
        configurations.schemas.collect { zipTree(it) }
    }
    into generatedResourcesDir
    exclude "META-INF/**"
}

processResources.dependsOn(extractSchemas)

clean {
    doLast {
        if (generatedResourcesDir.exists()) {
            generatedResourcesDir.listFiles().each { f -> delete f }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

task tagRelease(type: Exec) {
    commandLine "git"
    args "tag", "-a", "$version", "-m", "Created release $version"
}

task tagReleaseRemote(type: Exec) {
    commandLine "git"
    args "push", "origin", "$version"
}

task untagRelease(type: Exec) {
    commandLine "git"
    args "tag", "-d", "$version"
}

task untagReleaseRemote(type: Exec) {
    commandLine "git"
    args "push", "-d", "origin", "$version"
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "commons-templates-schemas"
                packaging "jar"
                // optionally artifactId can be defined here
                description "XML base schemas used for interfacing with Glookast Products"
                url "http://github.com/glookast/commons-templates-schemas"

                scm {
                    connection "scm:git:git://github.com/glookast/commons-templates-schemas.git"
                    developerConnection "scm:git:ssh://github.com:glookast/commons-templates-schemas.git"
                    url "http://github.com/glookast/commons-templates-schemas/tree/master"
                }

                licenses {
                    license {
                        name "The Apache License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id "joostrovers"
                        name "Joost Rovers"
                        email "joost.rovers@glookast.com"
                    }
                }
            }
        }
    }
}